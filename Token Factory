// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

interface ILockedTokenContract {
    function balanceOf(address account) external view returns (uint256);
}

interface IVestedTokenContract {
    function balanceOf(address account) external view returns (uint256);
}

interface IStakingTokenContract {
    function balanceOf(address account) external view returns (uint256);
}

interface ITokenFactory {
    function updateCreationFee(uint256 newFee) external;
    function updateFeeRecipient(address payable newRecipient) external;
}

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract TokenFactory is Ownable, ReentrancyGuard {
    uint256 public creationFee;
    address payable public feeRecipient;
    address public governanceContract;
    mapping(address => address[]) public tokensCreatedBy;
    error UnauthorizedCaller();

    event TokenCreated(
        address indexed creator,
        address indexed tokenAddress,
        string tokenName,
        string tokenSymbol,
        uint256 indexed decimals,
        uint256 totalSupply
    );
    
    constructor(uint256 _creationFee, address payable _feeRecipient, address _governanceContract) {
        require(_feeRecipient != address(0), "INVALID_RECIPIENT");
        creationFee = _creationFee;
        feeRecipient = _feeRecipient;
        governanceContract = _governanceContract;
    }

    modifier onlyGovernance() {
        //require(_msgSender() == governanceContract, "Caller is not the governance contract");
        if (_msgSender() != governanceContract) revert UnauthorizedCaller();
        _;
    }

    function updateCreationFee(uint256 _newFee) public onlyGovernance {
        creationFee = _newFee;
    }

    function updateFeeRecipient(address payable _newRecipient) public onlyGovernance {
        require(_newRecipient != address(0), "INVALID_RECIPIENT");
        feeRecipient = _newRecipient;
    }

    function updateGovContract(address _newContract) public onlyGovernance {
        require(_newContract != address(0), "INVALID_RECIPIENT");
        governanceContract = _newContract;
    }

    function createToken(
        string memory name,
        string memory symbol,
        uint256 maximumSupply,
        uint8 decimals,
        uint256 buyTax,
        uint256 sellTax,
        uint256 burnTax,
        address treasury
        ) public payable nonReentrant {
        require(msg.value == creationFee, "INCORRECT_FEES");
        require(buyTax <= 10 && sellTax <= 10 && burnTax <= 10, "TAX SHOULD BE <= 10%");
        require(treasury != address(0), "INVALID_RECIPIENT");
        feeRecipient.transfer(msg.value);

        uint256 supplyWithDecimals = maximumSupply * (10 ** uint256(decimals));
        NewToken mintToken = new NewToken(
            name,
            symbol,
            supplyWithDecimals,
            _msgSender(),
            decimals,
            buyTax,
            sellTax,
            burnTax,
            treasury,
            governanceContract
        );
        mintToken.transferOwnership(_msgSender());
        tokensCreatedBy[_msgSender()].push(address(mintToken));
        emit TokenCreated(_msgSender(), address(mintToken), name, symbol, decimals, supplyWithDecimals);
    }

    function renounceOwnership() public override onlyOwner {
        super.renounceOwnership();
    }

    function getTokensCreatedBy(address creator) public view returns (address[] memory) {
        return tokensCreatedBy[creator];
    }

}

// NewToken extends the ERC20 implementation from OpenZeppelin and adds a burn function.
contract NewToken is ERC20, ERC20Burnable, Ownable, ReentrancyGuard {
    uint256 private _maximumSupply;
    uint8 private _customDecimals;
    bool private _mintingEnabled = true; // Minting is enabled by default
    address public lockedTokenContract;
    address public vestedTokenContract;
    address public stakingTokenContract;
    uint256 public buyTax;
    uint256 public sellTax;
    address public treasury;
    uint256 public burnTax;
    address public governanceContract;
    
    error UnauthorizedCaller();
    
    event MintingDisabled();
    event TreasuryUpdate(address indexed newTreasury);
    event BuyTaxUpdated(uint256 _newTaxPercent);
    event SellTaxUpdated(uint256 _newTaxPercent);
    event BurnTaxUpdated(uint256 _newTaxPercent);
    event BuyTaxTransferred(address indexed from, address indexed to, uint256 taxAmount);
    event SellTaxTransferred(address indexed from, address indexed to, uint256 taxAmount);
    event LockedTokenContractUpdated(address indexed newLockedTokenContract);
    event VestedTokenContractUpdated(address indexed newVestedTokenContract);
    event StakingTokenContractUpdated(address indexed newStakingTokenContract);

    mapping(address => bool) public isLiquidityPool;

    constructor(
        string memory name,
        string memory symbol,
        uint256 maxSupply,
        address tokenOwner,
        uint8 Decimals,
        uint256 _buyTax,
        uint256 _sellTax,
        uint256 _burnTax,
        address _treasury,
        address _governanceContract
    ) ERC20(name, symbol) {
        require(tokenOwner != address(0), "INVALID_RECIPIENT");
        require(_treasury != address(0), "INVALID_RECIPIENT");
        require(_buyTax <= 10 && _sellTax <= 10 && _burnTax <= 10, "TAX SHOULD BE <= 10%");
        
        _customDecimals = Decimals;
        _maximumSupply = maxSupply;
        _mint(tokenOwner, maxSupply);
        buyTax = _buyTax;
        sellTax = _sellTax;
        burnTax = _burnTax;
        governanceContract = _governanceContract;
    }

    modifier onlyGovernance() {
        //require(_msgSender() == governanceContract, "Caller is not the governance contract");
        if (_msgSender() != governanceContract) revert UnauthorizedCaller();
        _;
    }

    function setLockedTokenContract(address _lockedTokenContract) public onlyOwner {
        lockedTokenContract = _lockedTokenContract;
        emit LockedTokenContractUpdated(_lockedTokenContract);
    }

    function setVestedTokenContract(address _vestedTokenContract) public onlyOwner {
        vestedTokenContract = _vestedTokenContract;
        emit VestedTokenContractUpdated(_vestedTokenContract);
    }

    function setStakingTokenContract(address _stakingTokenContract) public onlyOwner {
        stakingTokenContract = _stakingTokenContract;
        emit StakingTokenContractUpdated(_stakingTokenContract);
    }
    
    function updateBuyTax(uint256 _newTaxPercent) public onlyGovernance {
        require(_newTaxPercent <= 20, "NEW TAX SHOULD BE <= 20%");
        buyTax = _newTaxPercent;
        emit BuyTaxUpdated(_newTaxPercent);
    }

    function updateSellTax(uint256 _newTaxPercent) public onlyGovernance {
        require(_newTaxPercent <= 20, "NEW TAX SHOULD BE <= 20%");
        sellTax = _newTaxPercent;
        emit SellTaxUpdated(_newTaxPercent);
    }

    function updateBurnTax(uint256 _newTaxPercent) public onlyGovernance {
        require(_newTaxPercent <= 20, "NEW TAX SHOULD BE <= 20%");
        burnTax = _newTaxPercent;
        emit BurnTaxUpdated(_newTaxPercent);
    }

    function updateTreasury(address _treasury) public onlyGovernance {
        require(_treasury != address(0), "INVALID_RECIPIENT");
        treasury = _treasury;
        emit TreasuryUpdate(_treasury);
    }

    function addLiquidityPool(address _pool) public onlyGovernance {
        isLiquidityPool[_pool] = true;
    }

    function removeLiquidityPool(address _pool) public onlyGovernance {
        isLiquidityPool[_pool] = false;
    }
    
    function mint(address to, uint256 amount) public onlyGovernance nonReentrant {
        require(_mintingEnabled, "MINTING_DISABLED");
        require(totalSupply() + amount <= _maximumSupply, "CANNOT EXCEED MAXIMUM SUPPLY");
        _mint(to, amount);
    }

    function disableMinting() public onlyGovernance {
        _mintingEnabled = false;
        emit MintingDisabled();
    }

    // Override the _transfer function to include fee logic
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal override {
        uint256 taxAmount = 0;
        uint256 burnAmount = 0;
        
    // Check and apply buy fee and/or burn fee
        if (_isBuying(from)) {
            if (buyTax > 0) {
                taxAmount = (amount * buyTax) / 100;
                super._transfer(from, treasury, taxAmount);
            }
            if (burnTax > 0) {
            burnAmount = (amount * burnTax) / 100;
            _burn(from, burnAmount);
            }
            emit BuyTaxTransferred(from, treasury, taxAmount);
            amount -= (taxAmount + burnAmount);
        }
        
    // Check and apply sell fee and/or burn fee
        if (_isSelling(to)) {
            if (sellTax > 0) {
            taxAmount = (amount * sellTax) / 100;
            super._transfer(from, treasury, taxAmount);
            }
        if (burnTax > 0) {
            burnAmount = (amount * burnTax) / 100;
            _burn(from, burnAmount);
        }
            emit SellTaxTransferred(from, treasury, taxAmount);
            amount -= (taxAmount + burnAmount);
        }

        super._transfer(from, to, amount);
    }

    function renounceOwnership() public override onlyOwner {
        super.renounceOwnership();
    }
    
    function maximumSupply() public view returns (uint256) {
        return _maximumSupply;
    }

    function circulatingSupply() public view returns (uint256) {
        uint256 lockedBalance = 0;
        uint256 vestedBalance = 0;
        uint256 stakingBalance = 0;

        if (lockedTokenContract != address(0)) {
            lockedBalance = ILockedTokenContract(lockedTokenContract).balanceOf(address(this));
        }

        if (vestedTokenContract != address(0)) {
            vestedBalance = IVestedTokenContract(vestedTokenContract).balanceOf(address(this));
        }

        if (stakingTokenContract != address(0)) {
            stakingBalance = IStakingTokenContract(stakingTokenContract).balanceOf(address(this));
        }

        return totalSupply() - (lockedBalance + vestedBalance + stakingBalance);
    }

    function decimals() public view virtual override returns (uint8) {
        return _customDecimals;
    }

    function mintStatus() public view returns (bool) {
        return _mintingEnabled;
    }

    function _isBuying(address from) private view returns (bool) {
        return isLiquidityPool[from];
    }

    function _isSelling(address to) private view returns (bool) {
        return isLiquidityPool[to];
    }

}
