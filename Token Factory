// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

interface ILockedTokenContract {
    function balanceOf(address account) external view returns (uint256);
}

interface IVestedTokenContract {
    function balanceOf(address account) external view returns (uint256);
}

interface IStakingTokenContract {
    function balanceOf(address account) external view returns (uint256);
}

interface ITokenFactory {
    function updateCreationFee(uint256 newFee) external;
    function updateFeeRecipient(address payable newRecipient) external;
}

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract TokenFactory is Ownable, ReentrancyGuard {
    uint256 public creationFee;
    address payable public feeRecipient;
    address public governanceContract;
    mapping(address => address[]) private tokensCreatedBy;

    event TokenCreated(
        address indexed creator,
        address indexed tokenAddress,
        string tokenName,
        string tokenSymbol,
        uint256 indexed decimals,
        uint256 totalSupply
    );
    
    constructor(uint256 _creationFee, address payable _feeRecipient, address _governanceContract) {
        require(_feeRecipient != address(0), "Fee recipient cannot be the zero address");
        creationFee = _creationFee;
        feeRecipient = _feeRecipient;
        governanceContract = _governanceContract;
    }

    modifier onlyGovernance() {
        require(msg.sender == governanceContract, "Caller is not the governance contract");
        _;
    }

    function createToken(
        string memory name,
        string memory symbol,
        uint256 maximumSupply,
        uint8 decimals,
        uint256 buyFeePercent,
        uint256 sellFeePercent,
        uint256 burnFeePercent,
        address treasury
        ) public payable nonReentrant {
        require(msg.value == creationFee, "Incorrect Fees");
        require(buyFeePercent <= 10 && sellFeePercent <= 10, "Buy / Sell fee percent must not exceed 10%");
        require(burnFeePercent <= buyFeePercent && burnFeePercent <= sellFeePercent, "Burn fee must not exceed buy / sell fee percent");
        //require(treasury != address(0), "Treasury address cannot be zero");   //Removed due to file size restrictions
        // Send the creation fee to the fee recipient
        feeRecipient.transfer(msg.value);

        uint256 supplyWithDecimals = maximumSupply * (10 ** uint256(decimals));
        NewToken mintToken = new NewToken(
            name,
            symbol,
            supplyWithDecimals,
            _msgSender(),
            decimals,
            buyFeePercent,
            sellFeePercent,
            burnFeePercent,
            treasury,
            governanceContract
        );
        mintToken.transferOwnership(_msgSender());
        tokensCreatedBy[_msgSender()].push(address(mintToken));
        emit TokenCreated(_msgSender(), address(mintToken), name, symbol, decimals, supplyWithDecimals);
    }

    function updateCreationFee(uint256 newFee) external onlyGovernance {
        creationFee = newFee;
    }

    function updateFeeRecipient(address payable newRecipient) external onlyGovernance {
        require(newRecipient != address(0), "New recipient cannot be the zero address");
        feeRecipient = newRecipient;
    }

    function updateGovContract(address newContract) external onlyGovernance {
        require(newContract != address(0), "New contract cannot be the zero address");
        governanceContract = newContract;
    }

    function getTokensCreatedBy(address creator) public view returns (address[] memory) {
        return tokensCreatedBy[creator];
    }

    function renounceOwnership() public override onlyOwner {
        super.renounceOwnership();
    }

}

// NewToken extends the ERC20 implementation from OpenZeppelin and adds a burn function.
contract NewToken is ERC20, ERC20Burnable, Ownable, ReentrancyGuard {
    uint256 private _maximumSupply;
    uint8 private _customDecimals;
    bool private _mintingEnabled = true; // Minting is enabled by default
    address public lockedTokenContract;
    address public vestedTokenContract;
    address public stakingTokenContract;
    uint256 public buyFeePercent;
    uint256 public sellFeePercent;
    address public treasury;
    uint256 public burnFeePercent;
    address public governanceContract;
    
    event MintingDisabled();
    event TreasuryUpdate(address indexed newTreasury);
    event BuyFeeUpdated(uint256 newFeePercent);
    event SellFeeUpdated(uint256 newFeePercent);
    event BuyFeeTransferred(address indexed from, address indexed to, uint256 feeAmount);
    event SellFeeTransferred(address indexed from, address indexed to, uint256 feeAmount);

    mapping(address => bool) public isLiquidityPool;

    constructor(
        string memory name,
        string memory symbol,
        uint256 maxSupply,
        address tokenOwner,
        uint8 Decimals,
        uint256 _buyFeePercent,
        uint256 _sellFeePercent,
        uint256 _burnFeePercent,
        address _treasury,
        address _governanceContract
    ) ERC20(name, symbol) {
        require(tokenOwner != address(0), "Token owner cannot be the zero address");
        require(_treasury != address(0), "Treasury address cannot be zero");
        require(_buyFeePercent <= 10, "Buy fee percent must not exceed 10%");
        require(_sellFeePercent <= 10, "Sell fee percent must not exceed 10%");
        require(_burnFeePercent <= _buyFeePercent, "Buy burn fee must not exceed buy fee percent");
        require(_burnFeePercent <= _sellFeePercent, "Sell burn fee must not exceed sell fee percent");
        
        _customDecimals = Decimals;
        _maximumSupply = maxSupply;
        _mint(tokenOwner, maxSupply);
        buyFeePercent = _buyFeePercent;
        sellFeePercent = _sellFeePercent;
        burnFeePercent = _burnFeePercent;
        governanceContract = _governanceContract;
    }

    modifier onlyGovernance() {
        require(msg.sender == governanceContract, "Caller is not the governance contract");
        _;
    }

    function mint(address to, uint256 amount) public onlyGovernance nonReentrant {
        require(_mintingEnabled, "Minting has been disabled");
        require(totalSupply() + amount <= _maximumSupply, "Cannot exceed Maximum Supply");
        _mint(to, amount);
    }

    function disableMinting() public onlyGovernance {
        _mintingEnabled = false;
        emit MintingDisabled();
    }

    function setLockedTokenContract(address _lockedTokenContract) external onlyOwner {
        lockedTokenContract = _lockedTokenContract;
    }

    function setVestedTokenContract(address _vestedTokenContract) external onlyOwner {
        vestedTokenContract = _vestedTokenContract;
    }

    function setStakingTokenContract(address _stakingTokenContract) external onlyOwner {
        stakingTokenContract = _stakingTokenContract;
    }
    
    function updateBuyFeePercent(uint256 newFeePercent) external onlyGovernance {
        require(newFeePercent <= 20, "Fee cannot exceed 20%");
        buyFeePercent = newFeePercent;
        emit BuyFeeUpdated(newFeePercent);
    }

    function updateSellFeePercent(uint256 newFeePercent) external onlyGovernance {
        require(newFeePercent <= 20, "Fee cannot exceed 20%");
        sellFeePercent = newFeePercent;
        emit SellFeeUpdated(newFeePercent);
    }

    function updateTreasury(address _treasury) public onlyGovernance {
        require(_treasury != address(0), "Treasury address cannot be zero");
        treasury = _treasury;
        emit TreasuryUpdate(_treasury);
    }

    // Override the _transfer function to include fee logic
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal override {
        uint256 feeAmount = 0;
        uint256 burnAmount = 0;
        
    // Check and apply buy fee and/or burn fee
        if (_isBuying(from)) {
            if (buyFeePercent > 0) {
                feeAmount = (amount * (buyFeePercent - burnFeePercent)) / 100;
                super._transfer(from, treasury, feeAmount);
            }
            if (burnFeePercent > 0) {
            burnAmount = (amount * burnFeePercent) / 100;
            _burn(from, burnAmount);
            }
            emit BuyFeeTransferred(from, treasury, feeAmount);
            amount -= (feeAmount + burnAmount);
        }
        
    // Check and apply sell fee and/or burn fee
        if (_isSelling(to)) {
            if (sellFeePercent > 0) {
            feeAmount = (amount * (sellFeePercent - burnFeePercent)) / 100;
            super._transfer(from, treasury, feeAmount);
            }
        if (burnFeePercent > 0) {
            burnAmount = (amount * burnFeePercent) / 100;
            _burn(from, burnAmount);
        }
            emit SellFeeTransferred(from, treasury, feeAmount);
            amount -= (feeAmount + burnAmount);
        }

        super._transfer(from, to, amount);
    }
    
    function addLiquidityPool(address _pool) external onlyGovernance {
        isLiquidityPool[_pool] = true;
    }

    function removeLiquidityPool(address _pool) external onlyGovernance {
        isLiquidityPool[_pool] = false;
    }

    function _isBuying(address from) private view returns (bool) {
        return isLiquidityPool[from];
    }

    function _isSelling(address to) private view returns (bool) {
        return isLiquidityPool[to];
    }

    
    function maximumSupply() public view returns (uint256) {
        return _maximumSupply;
    }

    function circulatingSupply() public view returns (uint256) {
        uint256 lockedBalance = 0;
        uint256 vestedBalance = 0;
        uint256 stakingBalance = 0;

        if (lockedTokenContract != address(0)) {
            lockedBalance = ILockedTokenContract(lockedTokenContract).balanceOf(address(this));
        }

        if (vestedTokenContract != address(0)) {
            vestedBalance = IVestedTokenContract(vestedTokenContract).balanceOf(address(this));
        }

        if (stakingTokenContract != address(0)) {
            stakingBalance = IStakingTokenContract(stakingTokenContract).balanceOf(address(this));
        }

        return totalSupply() - (lockedBalance + vestedBalance + stakingBalance);
    }

    function decimals() public view virtual override returns (uint8) {
        return _customDecimals;
    }

    function mintStatus() public view returns (bool) {
        return _mintingEnabled;
    }

    function renounceOwnership() public override onlyOwner {
        super.renounceOwnership();
    }
}
